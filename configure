#!/bin/csh
#!/bin/csh -fx

# cheap hack to make things portable (but, note this is csh!)

# what versions of TCL library do we look for?
# (this is what needs to change in order to use a different version of Tcl.)
set nmtclxy=tcl80
set nmtclxDOTy=tcl8.0

set includelocs = (/usr/include /usr/local/include /usr/pkg/include ../../usr/include)

set gcc
set SVR4
set tcllibdir
set tclxy
set tclloc
set tclincl

# determine gcc status
gcc -v >& /dev/null
if ( $status == 0 ) set gcc='-e s/^#have-gcc#//'

# determine if SVR4
if ( `uname -s` == "SunOS" ) then
	if ( `uname -r | sed "s/\..*//"` == "5" ) set SVR4='-e s/^#have-SVR4#//'
	if ( $gcc == "" ) echo "compile errors?  maybe because you need an ANSI-compatible compiler like gcc..."
endif

# determine name of TCL library and lib directory...
set ld_path="/lib:/usr/lib:/usr/local/lib:/usr/X11/lib:/usr/pkg/lib"
if ($?LD_LIBRARY_PATH) then
    set ld_path=($ld_path $LD_LIBRARY_PATH)
endif

foreach i (`echo $ld_path | sed "s/:/ /g"`)
    foreach j (${nmtclxDOTy} ${nmtclxy})
        ls ${i}/lib${j}* >& /dev/null
        if ($status == 0) then
            set tcllibdir="-e sY__CONFIGURE__TCLLIBDIR__Y${i}Y"
            set tclxy="-e s/__CONFIGURE__TCLXY__/${j}/"
        endif
    end
end

if ("$tclxy" == "") then
    echo "Cannot find lib$nmtclxDOTy.a or lib$nmtclxy.a!  Aborting..."
    echo "(to change to a different version of tcl, edit 'configure')"
    exit 1
endif

foreach i (`echo $ld_path | sed "s/:/ /g"`)
    foreach j (${i}/${nmtclxDOTy} ${i}/${nmtclxy})
        if (-e ${j}/init.tcl) then
            set tclloc="-e sY__CONFIGURE__TCLINCLDIR__Y${j}Y"
        endif
    end
end

if ("$tclloc" == "") then
    echo "Cannot find init.tcl from Tcl distribution!  Aborting..."
    echo "(to change the searched directories, edit ``configure'')"
    exit 1
endif    

foreach i ( ${includelocs} )
    foreach j ( ${i} ${i}/${nmtclxy} ${i}/${nmtclxDOTy} )
        if (-e ${j}/tcl.h) then
            if ("$tclincl" == "") then
                set tclincl="-e sY__CONFIGURE__TCLINCL__Y${j}Y"
            endif
        endif
    end
end

if ("$tclincl" == "") then
    echo "Cannot find tcl.h!  Aborting..."
    echo "(to change the searched directories, edit ``configure'')"
    exit 1
endif
    
    
            

# prepare Makefile
echo "# THIS FILE IS CREATED BY ./configure -- DO NOT HAND EDIT!" > Makefile
echo "# (Edit Makefile.in and then rerun ./configure instead.)" >> Makefile

# now, edit Makefile.in...
sed -e 's/^#have-none#//' $gcc $SVR4 $tcllibdir $tclxy $tclloc $tclincl Makefile.in >> Makefile
chmod ug+w Makefile
